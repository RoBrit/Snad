plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.142'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

def mcBase = getMinecraftBaseVersion()

group = 'com.robrit.snad'
version = "${mcBase}.${mod_version}"

base {
    archivesName = "Snad"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)


runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
        programArgument '--nogui'
    }

    clientData {
        workingDirectory project.file('run-data')
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {

}

dependencies {
    implementation "net.neoforged:neoforge:${forge_version}"
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "Snad",
                "Specification-Vendor"    : "RoBrit",
                "Specification-Version"   : "${mod_version}",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "RoBrit",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version_range: forge_version_range,
            loader_version: loader_version,
            mod_id: mod_id,
            version: project.version,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

//publishing {
//    publications {
//        register('mavenJava', MavenPublication) {
//            artifact jar
////            artifactId = project.name.toLowerCase()
//        }
//    }
//
//    repositories {
//        def actor = providers.environmentVariable("GITHUB_ACTOR")
//        if (actor.isPresent()) {
//            maven {
//                name = "GitHubPackages"
//                url = "https://maven.pkg.github.com/RoBrit/Snad"
//                credentials {
//                    username = actor.get()
//                    password = providers.environmentVariable("GITHUB_TOKEN").get()
//                }
//            }
//        }
//    }
//}

publishMods {
    dryRun = providers.environmentVariable("CURSE_TOKEN").getOrNull() == null
    changelog = file("./CHANGELOG.md").text
    version = project.version
    file = project.provider { project.tasks.jar }.flatMap { it.archiveFile }
    modLoaders.add("neoforge")
    type = STABLE
    displayName = "[${minecraft_version}] ${base.archivesName.get()} ${project.version}"

    curseforge {
        projectId = curseforge_id
        accessToken = providers.environmentVariable("CURSE_TOKEN")
        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

//#region helpers
/**
 * Removes the 1. from Minecraft's version, then adds a 0 to the end if the
 * version does not contain three dots.
 */
def getMinecraftBaseVersion() {
    def version = project.minecraft_version
    def parts = version.split("\\.")

    // Remove the first part of the version
    parts = parts[1..-1]

    if (parts.size() < 2) {
        parts << "0"
    }

    return parts.join(".")
}
//#endregion
